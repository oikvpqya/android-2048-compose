import org.jetbrains.compose.desktop.application.dsl.TargetFormat

plugins {
    alias libs.plugins.android.application
    alias libs.plugins.compose.compiler
    alias libs.plugins.jetbrains.compose
    alias libs.plugins.kotlin.multiplatform
    alias libs.plugins.kotlin.parcelize
    alias libs.plugins.kotlin.serialization
}

kotlin {
    jvmToolchain(17)

    androidTarget() {
        compilerOptions {
            freeCompilerArgs.addAll(
                "-P",
                "plugin:org.jetbrains.kotlin.parcelize:additionalAnnotation=CommonParcelize",
            )
        }
    }

    jvm("desktop")

    wasmJs {
        moduleName = "composeApp"
        browser {
            commonWebpackConfig {
                outputFileName = "composeApp.js"
            }
        }
        binaries.executable()
    }

    sourceSets {
        androidMain {
            dependencies {
                implementation libs.androidx.core.ktx
                implementation libs.androidx.activity.compose
                implementation libs.kotlinx.coroutines.android
                implementation libs.xxfast.kstore.file
            }
        }

        commonMain {
            dependencies {
                implementation compose.material
                implementation compose.ui
                implementation libs.circuit.foundation
                implementation libs.kotlinx.coroutines.core
                implementation libs.kotlinx.serialization.json
                implementation libs.squareup.okio
                implementation libs.xxfast.kstore.common
            }
        }

        desktopMain {
            dependencies {
                implementation compose.desktop.currentOs
                implementation libs.harawata.appdirs
                implementation libs.kotlinx.coroutines.swing
                implementation libs.xxfast.kstore.file
            }
        }

        wasmJsMain {
            dependencies {
                implementation libs.xxfast.kstore.storage
            }
        }
    }
}

android {
    compileSdk 34
    namespace "com.alexjlockwood.twentyfortyeightcompose"

    defaultConfig {
        applicationId "com.alexjlockwood.twentyfortyeightcompose"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0.0"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    buildFeatures {
        compose true
    }
}

compose {
    desktop {
        application {
            mainClass = "MainKt"

            nativeDistributions {
                targetFormats(TargetFormat.Dmg, TargetFormat.Msi, TargetFormat.Deb)
                packageName = "com.alexjlockwood.twentyfortyeightcompose"
                packageVersion = "1.0.0"
            }
        }
    }

    experimental {
        web.application {}
    }
}
