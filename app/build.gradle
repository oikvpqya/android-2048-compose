import org.jetbrains.compose.desktop.application.dsl.TargetFormat

plugins {
    alias libs.plugins.android.application
    alias libs.plugins.compose.compiler
    alias libs.plugins.jetbrains.compose
    alias libs.plugins.kotlin.multiplatform
    alias libs.plugins.kotlin.serialization
}

kotlin {
    jvmToolchain(17)
    androidTarget()
    jvm("desktop")

    sourceSets {
        androidMain {
            dependencies {
                implementation libs.androidx.core.ktx
                implementation libs.androidx.activity.compose
                implementation libs.kotlinx.coroutines.android
            }
        }

        commonMain {
            dependencies {
                implementation compose.foundation
                implementation compose.material3
                implementation compose.ui
                implementation libs.androidx.datastore.preferences
                implementation libs.androidx.lifecycle.viewmodel.compose
                implementation libs.kotlinx.coroutines.core
                implementation libs.kotlinx.serialization.json
            }
        }

        desktopMain {
            dependencies {
                implementation compose.desktop.currentOs
                implementation libs.kotlinx.coroutines.swing
            }
        }
    }
}

android {
    compileSdk 34
    buildToolsVersion = "34.0.0"
    namespace 'com.alexjlockwood.twentyfortyeight'

    defaultConfig {
        applicationId "com.alexjlockwood.twentyfortyeightcompose"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0.0"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.getByName("debug")
        }
    }

    buildFeatures {
        compose true
    }
}

compose {
    desktop {
        application {
            mainClass = "MainKt"

            nativeDistributions {
                targetFormats(TargetFormat.Dmg, TargetFormat.Msi, TargetFormat.Deb)
                packageName = "com.alexjlockwood.twentyfortyeightcompose"
                packageVersion = "1.0.0"
            }
        }
    }
}
