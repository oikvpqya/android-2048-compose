import java.nio.file.*
import org.jetbrains.compose.desktop.application.dsl.TargetFormat

plugins {
    alias libs.plugins.android.application
    alias libs.plugins.cash.licensee
    alias libs.plugins.compose.compiler
    alias libs.plugins.jetbrains.compose
    alias libs.plugins.kotlin.multiplatform
    alias libs.plugins.kotlin.serialization
}

kotlin {
    jvmToolchain(17)
    androidTarget()
    jvm("desktop")

    wasmJs {
        moduleName = "composeApp"
        browser {
            commonWebpackConfig {
                outputFileName = "composeApp.js"
            }
        }
        binaries.executable()
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation compose.components.resources
                implementation compose.material
                implementation compose.ui
                implementation libs.kotlinx.coroutines.core
                implementation libs.kotlinx.serialization.json
                implementation libs.takahirom.rin
                implementation libs.xxfast.kstore.common
            }
        }

        androidMain {
            dependencies {
                implementation libs.androidx.core.ktx
                implementation libs.androidx.activity.compose
                implementation libs.squareup.okio
                implementation libs.xxfast.kstore.file
            }
        }

        desktopMain {
            dependencies {
                implementation compose.desktop.currentOs
                implementation libs.harawata.appdirs
                implementation libs.squareup.okio
                implementation libs.xxfast.kstore.file
            }
        }

        wasmJsMain {
            dependencies {
                implementation libs.xxfast.kstore.storage
            }
        }

        commonTest {
            dependencies {
                implementation libs.cash.molecule
                implementation libs.cash.turbine
                implementation libs.jetbrains.lifecycle.runtime.compose
                implementation libs.jetbrains.lifecycle.viewmodel.compose
                implementation libs.kotlin.test
                implementation libs.kotlinx.coroutines.test
            }
        }
    }
}

android {
    compileSdk 34
    namespace "com.alexjlockwood.twentyfortyeightcompose"

    defaultConfig {
        applicationId "com.alexjlockwood.twentyfortyeightcompose"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0.0"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.getByName("debug")
        }
    }

    buildFeatures {
        compose true
    }
}

compose {
    desktop {
        application {
            mainClass = "MainKt"

            nativeDistributions {
                targetFormats(TargetFormat.Dmg, TargetFormat.Msi, TargetFormat.Deb)
                packageName = "com.alexjlockwood.twentyfortyeightcompose"
                packageVersion = "1.0.0"
            }
        }
    }

    experimental {
        web.application {}
    }

    resources {
        packageOfResClass = "com.alexjlockwood.twentyfortyeight"
        customDirectory(
            "androidMain",
            layout.buildDirectory.dir("generated/licensee/androidRelease")
        )
        customDirectory(
            "desktopMain",
            layout.buildDirectory.dir("generated/licensee/desktop")
        )
        customDirectory(
            "wasmJsMain",
            layout.buildDirectory.dir("generated/licensee/wasmJs")
        )
    }
}

licensee {
    allow "Apache-2.0"
    allowUrl("https://opensource.org/licenses/Apache-2.0.html") {
        // for takahirom Rin
        because "Apache-2.0, but self-hosted copy of the license"
    }
}

tasks.register("copyArtifactsJsonFiles") {
    String[] dirs = ["androidRelease", "desktop", "wasmJs"]
    String buildDirectory = layout.buildDirectory.get().toString()
    for(dir in dirs) {
        Path from = Paths.get(buildDirectory, "reports/licensee/$dir/artifacts.json")
        Path to = Paths.get(buildDirectory, "generated/licensee/$dir/files/artifacts.json")
        try {
            if (!Files.exists(to.parent)) {
                Files.createDirectories(to.parent)
            }
            Files.copy(from, to, StandardCopyOption.REPLACE_EXISTING)
        } catch(IOException e){
            System.out.println(e)
        }
    }
}
